with c;

pub type Fd {
    _file: *mut c::types::FILE,
}

pub fn Fd::write_u8(self: Fd, value: u8) {
    c::fputc(value as c::types::int, self._file);
}

pub fn Fd::write(self: Fd, data: []u8) {
    c::fwrite(&data[0] as *c::types::void, 1, data.len, self._file);
}

pub fn Fd::read_u8(self: Fd) ?u8 {
    let c = c::fgetc(self._file);

    if c == c::EOF {
        return null;
    }

    return c as u8;
}

pub fn print_rune(value: rune) {
    write_rune(stdout(), value);
}

pub fn eprint_rune(value: rune) {
    write_rune(stderr(), value);
}

pub fn print_uint(value: u64) {
    write_uint(stdout(), value);
}

pub fn eprint_uint(value: u64) {
    write_uint(stderr(), value);
}

pub fn print_int(value: i64) {
    write_int(stdout(), value);
}

pub fn eprint_int(value: i64) {
    write_int(stderr(), value);
}

pub fn print(str: []u8) {
    stdout().write(str);
}

pub fn eprint(str: []u8) {
    stderr().write(str);
}

pub fn println(str: []u8) {
    let fd = stdout();
    fd.write(str);
    fd.write_u8('\n' as u8);
}

pub fn eprintln(str: []u8) {
    let fd = stderr();
    fd.write(str);
    fd.write_u8('\n' as u8);
}

pub fn write_rune(fd: Fd, value: rune) {
    let r = value as u32;

    if r > 0x10ffff || (r >= 0xd800 && r <= 0xdfff) {
        return;
    }

    if r <= 0x7f {
        fd.write_u8((r) as u8);
        return;
    }

    if r <= 0x7ff {
        fd.write_u8((0xc0 | (r >> 6)) as u8);
        fd.write_u8((0x80 | (r & 0x3f)) as u8);
        return;
    }

    if r <= 0xffff {
        fd.write_u8((0xe0 | (r >> 12)) as u8);
        fd.write_u8((0x80 | ((r >> 6) & 0x3f)) as u8);
        fd.write_u8((0x80 | (r & 0x3f)) as u8);
        return;
    }

    fd.write_u8((0xf0 | (r >> 18)) as u8);
    fd.write_u8((0x80 | ((r >> 12) & 0x3f)) as u8);
    fd.write_u8((0x80 | ((r >> 6) & 0x3f)) as u8);
    fd.write_u8((0x80 | (r & 0x3f)) as u8);
}

pub fn write_uint(fd: Fd, mut value: u64) {
    mut buf: [20]u8 = undefined;
    mut i: usize;

    if value == 0 {
        fd.write_u8('0' as u8);
        return;
    }

    while value > 0 {
        buf[i] = ('0' as u8 + (value % 10) as u8);

        value /= 10;
        i += 1;
    }

    while i > 0 {
        fd.write_u8(buf[i - 1] as u8);
        i -= 1;
    }
}

pub fn write_int(fd: Fd, value: i64) {
    if value < 0 {
        fd.write_u8('-' as u8);
        write_uint(fd, (-(value + 1) + 1) as u64);
        return;
    }

    write_uint(fd, value as u64);
}

pub fn stdout() Fd {
    return Fd { _file: c::stdout };
}

pub fn stdin() Fd {
    return Fd { _file: c::stdin };
}

pub fn stderr() Fd {
    return Fd { _file: c::stderr };
}
