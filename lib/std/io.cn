with c;
with std::ffi;

pub type Fd {
    _file: *mut c::FILE,
}

pub fn Fd::close(self: Fd) {
    c::fclose(self._file);
}

pub fn print_rune(value: rune) {
    write_rune(stdout(), value);
}

pub fn eprint_rune(value: rune) {
    write_rune(stderr(), value);
}

pub fn print_uint(value: u64) {
    write_uint(stdout(), value);
}

pub fn eprint_uint(value: u64) {
    write_uint(stderr(), value);
}

pub fn print_int(value: i64) {
    write_int(stdout(), value);
}

pub fn eprint_int(value: i64) {
    write_int(stderr(), value);
}

pub fn print(str: []u8) {
    write(stdout(), str);
}

pub fn eprint(str: []u8) {
    write(stderr(), str);
}

pub fn println(str: []u8) {
    writeln(stdout(), str);
}

pub fn eprintln(str: []u8) {
    writeln(stderr(), str);
}

pub fn input_line_to(buf: []mut u8) ?[]mut u8 {
    return read_line_to(stdin(), buf);
}

pub fn input_uint() ?u64 {
    return read_uint(stdin());
}

pub fn input_int() ?i64 {
    return read_int(stdin());
}

pub fn write_u8(fd: Fd, byte: u8) {
    c::fputc(byte as c::int, fd._file);
}

pub fn write(fd: Fd, data: []u8) {
    c::fwrite(&data[0] as *c::void, 1, data.len, fd._file);
}

pub fn writeln(fd: Fd, data: []u8) {
    write(fd, data);
    write_u8(fd, '\n' as u8);
}

pub fn write_rune(fd: Fd, value: rune) {
    let r = value as u32;

    if r > 0x10ffff || (r >= 0xd800 && r <= 0xdfff) {
        return;
    }

    if r <= 0x7f {
        write_u8(fd, (r) as u8);
        return;
    }

    if r <= 0x7ff {
        write_u8(fd, (0xc0 | (r >> 6)) as u8);
        write_u8(fd, (0x80 | (r & 0x3f)) as u8);
        return;
    }

    if r <= 0xffff {
        write_u8(fd, (0xe0 | (r >> 12)) as u8);
        write_u8(fd, (0x80 | ((r >> 6) & 0x3f)) as u8);
        write_u8(fd, (0x80 | (r & 0x3f)) as u8);
        return;
    }

    write_u8(fd, (0xf0 | (r >> 18)) as u8);
    write_u8(fd, (0x80 | ((r >> 12) & 0x3f)) as u8);
    write_u8(fd, (0x80 | ((r >> 6) & 0x3f)) as u8);
    write_u8(fd, (0x80 | (r & 0x3f)) as u8);
}

pub fn write_uint(fd: Fd, mut value: u64) {
    mut buf: [20]u8 = undefined;
    mut i: usize;

    if value == 0 {
        write_u8(fd, '0' as u8);
        return;
    }

    while value > 0 {
        buf[i] = ('0' as u8 + (value % 10) as u8);

        value /= 10;
        i += 1;
    }

    while i > 0 {
        write_u8(fd, buf[i - 1] as u8);
        i -= 1;
    }
}

pub fn write_int(fd: Fd, value: i64) {
    if value < 0 {
        write_u8(fd, '-' as u8);
        write_uint(fd, (-(value + 1) + 1) as u64);
        return;
    }

    write_uint(fd, value as u64);
}

pub fn read_u8(fd: Fd) ?u8 {
    let c = c::fgetc(fd._file);

    if c == c::EOF {
        return null;
    }

    return c as u8;
}

pub fn read_line_to(fd: Fd, data: []mut u8) ?[]mut u8 {
    for i in 0..data.len {
        let c = read_u8(fd);

        if c == null {
            return data[:i];
        }

        let c = c.! as u8;

        if c == '\n' as u8 {
            return data[:i];
        }

        data[i] = c;
    }

    return null;
}

pub fn read_uint(fd: Fd) ?u64 {
    // TODO: do not rely on fscanf

    mut r: u64 = undefined;

    if c::fscanf(fd._file, ffi::as_cstr("%llu\0"), &r) != 1 {
        return null;
    }

    return r;
}

pub fn read_int(fd: Fd) ?i64 {
    // TODO: do not rely on fscanf

    mut r: i64 = undefined;

    if c::fscanf(fd._file, ffi::as_cstr("%lli\0"), &r) != 1 {
        return null;
    }

    return r;
}

pub fn stdout() Fd {
    return Fd { _file: c::stdout };
}

pub fn stdin() Fd {
    return Fd { _file: c::stdin };
}

pub fn stderr() Fd {
    return Fd { _file: c::stderr };
}
