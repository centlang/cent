with io;
with c;
with posix;

with std::alloc;
with std::ffi;

pub enum Mode u8 {
    read = 1 << 0,
    write = 1 << 1,
    append = 1 << 2,
    trunc = 1 << 3,
    create = 1 << 4,
}

pub fn open(path: []u8, mode: Mode) ?io::Fd {
    mut cstr: [path.len + 1]u8 = undefined;
    ffi::cstr_to(cstr, path);

    let file = c::fopen(ffi::as_cstr(cstr), ffi::as_cstr(to_libc_mode(mode)));

    if file == null {
        return null;
    }

    return io::Fd { _file: file.! };
}

#(linux)
pub fn mkdir(path: []u8) bool {
    mut cstr: [path.len + 1]u8 = undefined;
    ffi::cstr_to(cstr, path);

    return posix::mkdir(
        ffi::as_cstr(cstr), posix::S_IRWXU | posix::S_IRWXG | posix::S_IRWXO
    ) == 0;
}

fn to_libc_mode(mode: Mode) []u8 {
    if (mode & Mode::read) as u8 != 0 && (mode & Mode::write) as u8 == 0 {
        return "rb\0";
    }

    if (mode & Mode::read) as u8 == 0 && (mode & Mode::write) as u8 != 0 {
        if (mode & Mode::append) as u8 != 0 {
            return "ab\0";
        }

        return "wb\0";
    }

    if (mode & Mode::append) as u8 != 0 {
        return "a+b\0";
    }

    if (mode & Mode::trunc) as u8 != 0 {
        return "w+b\0";
    }

    return "r+b\0";
}
